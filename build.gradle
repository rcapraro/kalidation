plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
    id "org.jlleitschuh.gradle.ktlint" version "9.4.0"
    id "com.adarshr.test-logger" version "2.1.0"
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
    id "jacoco"
    id "org.jetbrains.dokka" version "1.4.0"
}

group = "com.capraro"
version = "1.5.0"

ext {
    junitJupiterVersion = "5.6.2"
    arrowVersion = "0.11.0"
    hibernateValidatorVersion = "6.1.5.Final"
    libphonenumberVersion = "8.12.9"
    groovyVersion = "3.0.5"
    assertJVersiom = "3.17.2"
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("org.hibernate:hibernate-validator:$hibernateValidatorVersion")
    compile("com.googlecode.libphonenumber:libphonenumber:$libphonenumberVersion")
    compile("org.glassfish:javax.el:3.0.1-b11")
    compile("org.apache.commons:commons-jexl3:3.1")
    compile("org.codehaus.groovy:groovy-jsr223:$groovyVersion")
    compile("io.arrow-kt:arrow-core:$arrowVersion")
    compile("io.arrow-kt:arrow-validation:$arrowVersion")
    testCompile("org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion")
    testRuntime("org.junit.platform:junit-platform-launcher:1.6.2")
    testCompile("org.assertj:assertj-core:$assertJVersiom")
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

wrapper {
    gradleVersion = '6.6.1'
}

jacoco {
    toolVersion = "0.8.5"
}

test {
    useJUnitPlatform()
}

def pomConfig = {
    licenses {
        license {
            name "The MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "rcapraro"
            name "Richard Capraro"
            email "richard.capraros@gmail.com"
        }
    }

    scm {
        url "https://github.com/rcapraro/kalidation"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId 'com.capraro'
            artifactId 'kalidation'
            version project.version
            artifact sourcesJar {
                classifier "sources"
            }
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'JSR380 Validation DSL in Kotlin')
                root.appendNode('name', 'Kalidation')
                root.appendNode('url', 'https://github.com/rcapraro/kalidation.git')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty("bintray.user")
    key = System.getProperty("bintray.key")
    publications = ['mavenPublication']
    pkg {
        repo = 'maven-releases'
        name = 'kalidation'
        userOrg = user
        licenses = ['MIT']
        labels = ['kotlin', 'dsl', 'jsr380']
        vcsUrl = 'https://github.com/rcapraro/kalidation.git'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released = new Date()
        }
        override = true
        publish = true
    }
}

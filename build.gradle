/*
 * The MIT License
 *
 * Copyright (c) 2018 Richard Capraro
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.60"
    id "com.adarshr.test-logger" version "1.4.0"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
    id "jacoco"
}

group = "com.capraro"
version = "1.0"

ext {
    kotlinVersion = '1.2.60'
    junitJupiterVersion = "5.2.0"
    arrowVersion = "0.7.2"
    hibernateValidatorVersion = "6.0.12.Final"
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("org.hibernate:hibernate-validator:$hibernateValidatorVersion")
    compile("com.googlecode.libphonenumber:libphonenumber:8.9.10")
    compile("org.glassfish:javax.el:3.0.1-b10")
    compile("io.arrow-kt:arrow-core:$arrowVersion")
    compile("io.arrow-kt:arrow-data:$arrowVersion")
    testCompile("org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion")
    testRuntime("org.junit.platform:junit-platform-launcher:1.2.0")
    testCompile("org.assertj:assertj-core:3.11.0")
}

repositories {
    jcenter()
    maven {
        //for jacoco snapshot
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

wrapper {
    gradleVersion = '4.9.0'
}

jacoco {
    toolVersion = "0.8.2-SNAPSHOT"
}

test {
    useJUnitPlatform()
}

def pomConfig = {
    licenses {
        license {
            name "The MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "rcapraro"
            name "Richard Capraro"
            email "richard.capraros@gmail.com"
        }
    }

    scm {
        url "https://github.com/rcapraro/kalidation"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId 'com.capraro'
            artifactId 'kalidation'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'JSR380 Validation DSL in Kotlin')
                root.appendNode('name', 'Kalidation')
                root.appendNode('url', 'https://github.com/rcapraro/kalidation.git')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty("bintray.user")
    key = System.getProperty("bintray.key")
    publications = ['mavenPublication']
    pkg {
        repo = 'maven-releases'
        name = 'kalidation'
        userOrg = user
        licenses = ['MIT']
        labels = ['kotlin', 'dsl', 'jsr380']
        vcsUrl = 'https://github.com/rcapraro/kalidation.git'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released = new Date()
        }
        publish = true
    }
}
